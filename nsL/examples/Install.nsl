#define Version '1.0.2'

Name("nsL Assembler ".Version." (Alpha)");
OutFile("nsL-Assembler-".Version.".exe");
XPStyle("on");
SetCompressor("lzma", true);
RequestExecutionLevel("admin");
ShowInstDetails("show");

page Components();
page Directory();
page InstFiles();
uninstall page UninstConfirm();
uninstall page InstFiles();

function .onInit()
{
  $INSTDIR = ReadRegStr("HKLM", @"Software\NSIS", "");
  if (StrCmp($INSTDIR, ""))
  {
    MessageBox("MB_OK|MB_ICONEXCLAMATION", "NSIS is not installed on this machine. Setup will now exit.");
    Abort();
  }
}

section Install("nsL Assembler", true)
{
  // Extract the main JAR file.
  SetOutPath($INSTDIR."\\NSL");
  File(@"..\dist\nsL.jar");

  // Extract documents.
  SetOutPath($INSTDIR."\\Docs\\NSL");
  File(@"..\..\Reference.pdf");
  File(@"..\..\functions.txt");

  // Add the right click "Compile nsL Script" option.
  WriteRegStr("HKCR", ".nsl", "", "nsL.Script");
  WriteRegStr("HKCR", "nsL.Script", "", "nsL Script File");
  WriteRegStr("HKCR", @"nsL.Script\DefaultIcon", "", $INSTDIR.@"\makensisw.exe,1");
  WriteRegStr("HKCR", @"nsL.Script\shell", "", "open");
  WriteRegStr("HKCR", @"nsL.Script\shell\open\command", "", 'notepad.exe "%1"');
  WriteRegStr("HKCR", @"nsL.Script\shell\compile", "", "Compile nsL Script");
  WriteRegStr("HKCR", @"nsL.Script\shell\compile\command", "", '"'.$INSTDIR.@'\NSL\nsL.bat" "%1"');

  // Write our batch file that runs scripts.
  ClearErrors();
  $R0 = FileOpen($INSTDIR.@"\NSL\nsL.bat", "w");
  if (!Errors())
  {
    FileWrite($R0, "cd %~dp0\r\n");
    FileWrite($R0, "java.exe -jar nsL.jar %1\r\n");
    FileClose($R0);
  }
  DetailPrint("Wrote: ".$INSTDIR.@"\NSL\nsL.bat");

  // Output the uninstall executable.
  WriteUninstaller($INSTDIR.@"\NSL\Uninstall.exe");

  // Refresh Windows Explorer.
  #define SHCNE_ASSOCCHANGED 0x8000000
  #define SHCNF_IDLIST 0
  System::Call(format("shell32::SHChangeNotify(i {0}, i {1}, i 0, i 0)", SHCNE_ASSOCCHANGED, SHCNF_IDLIST));
}

section Examples("Example Scripts")
{
  SetOutPath($INSTDIR."\\Examples\\NSL");
  FileRecursive(@"..\examples\*.nsl");
}

section Source("Java Source Code", false, true)
{
  SetOutPath($INSTDIR.@"\NSL\src");
  FileRecursive(@"..\nbproject");
  FileRecursive(@"..\src");
  FileRecursive(@"..\test");
  File(@"..\build.xml");
  File(@"..\manifest.mf");
  File(@"..\..\Reference.docx");
}

uninstall function .onInit()
{
  // Check we are uninstalling from the correct directory.
  if (!FileExists($INSTDIR."\\nsL.jar"))
  {
    MessageBox("MB_OK|MB_ICONEXCLAMATION", "The files to uninstall cannot be found. Setup will now exit.");
    Abort(); 
  }
}

uninstall section Uninstall()
{
  // Remove our files.
  RMDirRecursive($INSTDIR.@"\..\Docs\NSL");
  RMDirRecursive($INSTDIR);

  // Delete our Windows registry entries.
  DeleteRegKey("HKCR", ".nsl");
  DeleteRegKey("HKCR", "nsL.Script");
}
